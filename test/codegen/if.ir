; Copyright (C) 2024 Intel Corporation
; SPDX-License-Identifier: BSD-3-Clause

; RUN: %tinytc-oc < %s | filecheck %s
func @if0(%0: i32) {
    %c16 = constant 16 : i32
    %c0 = constant 0 : i32
    %1 = cmp.lt %0, %c16 : bool
    %2 = cmp.ge %0, %c0 : bool
    %3 = arith.and %1, %2 : bool
    if %3 {
    } else {
    }
; CHECK:      bool x1 = x < c16;
; CHECK:      bool x2 = x >= c0;
; CHECK:      bool x3 = x1 && x2;
; CHECK:      if (x3) {
; CHECK-NEXT: }
}

func @if1(%0: i32) {
    %c16 = constant 16 : i32
    %1 = cmp.lt %0, %c16 : bool
    if %1 {
    } else {
    }
; CHECK:      if (x1) {
; CHECK-NEXT: }
}

func @if2(%0: i32) {
    %c16 = constant 16 : i32
    %1 = cmp.lt %0, %c16 : bool
    if %1 -> () {
        yield ()
    } else {
        yield ()
    }
; CHECK:      if (x1) {
; CHECK-NEXT: } else {
; CHECK-NEXT: }
}

func @if3(%0: i32) {
    %c16 = constant 16 : i32
    %1 = cmp.lt %0, %c16 : bool
    %x = if %1 -> (i32) {
        yield (%0)
    } else {
        yield (%c16)
    }
; CHECK:      int x2;
; CHECK-NEXT: if (x1) {
; CHECK-NEXT:     x2 = x;
; CHECK-NEXT: } else {
; CHECK-NEXT:     x2 = c16;
; CHECK-NEXT: }
}

func @if4(%0: i32) {
    %c16 = constant 16 : i32
    %1 = cmp.lt %0, %c16 : bool
    %x, %y = if %1 -> (i32, f32) {
        if %1 {
        }
        %one = constant 1.0 : f32
        yield (%0, %one)
    } else {
        %z = if %1 -> (f32) {
            %one = constant 1.0 : f32
            yield (%one)
        } else {
            %zero = constant 0.0 : f32
            yield (%zero)
        }
        yield (%c16, %z)
    }
; CHECK:      int x2;
; CHECK-NEXT: float y;
; CHECK-NEXT: if (x1) {
; CHECK-NEXT:     if (x1) {
; CHECK-NEXT:     }
; CHECK-NEXT:     float one = 0x1p+0f;
; CHECK-NEXT:     x2 = x;
; CHECK-NEXT:     y = one;
; CHECK-NEXT: } else {
; CHECK-NEXT:     float z;
; CHECK-NEXT:     if (x1) {
; CHECK-NEXT:         float one = 0x1p+0f;
; CHECK-NEXT:         z = one;
; CHECK-NEXT:     } else {
; CHECK-NEXT:         float zero = 0x0p+0f;
; CHECK-NEXT:         z = zero;
; CHECK-NEXT:     }
; CHECK-NEXT:     x2 = c16;
; CHECK-NEXT:     y = z;
; CHECK-NEXT: }
}
