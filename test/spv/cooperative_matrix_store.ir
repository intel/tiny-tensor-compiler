; Copyright (C) 2024 Intel Corporation
; SPDX-License-Identifier: BSD-3-Clause

; RUN: %tinytc-oc -gspirv -S -O0 < %s | filecheck %s

; CHECK:      %[[#I32:]] = OpTypeInt 32 0
; CHECK:  %[[#I32_PTR:]] = OpTypePointer CrossWorkgroup %[[#I32]]
; CHECK:      %[[#I64:]] = OpTypeInt 64 0
; CHECK:  %[[#I64_C64:]] = OpConstant %[[#I64]] 64
; CHECK:  %[[#I64_C48:]] = OpConstant %[[#I64]] 48
; CHECK:   %[[#I64_C1:]] = OpConstant %[[#I64]] 1
; CHECK:     %[[#BOOL:]] = OpTypeBool


func @coopmatrix_a_store_n(%A: memref<i32x64x48>, %x: index, %y: index) subgroup_size(16) {
    %0 = constant 1 : coopmatrix<i32x16x3,matrix_a>
    cooperative_matrix_store %0, %A[%x,%y]
; CHECK:                        %[[#]] = OpFunction {{.*}}
; CHECK-NEXT:             %[[#T1_MR:]] = OpFunctionParameter %[[#I32_PTR]]
; CHECK-NEXT:              %[[#T1_X:]] = OpFunctionParameter %[[#I64]]
; CHECK-NEXT:              %[[#T1_Y:]] = OpFunctionParameter %[[#I64]]
; CHECK:            %[[#T1_X_STRIDE:]] = OpIMul %[[#I64]] %[[#T1_X]] %[[#I64_C1]]
; CHECK-NEXT:       %[[#T1_Y_STRIDE:]] = OpIMul %[[#I64]] %[[#T1_Y]] %[[#I64_C64]]
; CHECK-NEXT:        %[[#T1_OFFSET0:]] = OpIAdd %[[#I64]] %[[#T1_X_STRIDE]] %[[#T1_Y_STRIDE]]
; CHECK-NEXT:        %[[#T1_POINTER:]] = OpInBoundsPtrAccessChain %[[#I32_PTR]] %[[#T1_MR]] %[[#T1_OFFSET0]]
; CHECK:                   %[[#T1_M:]] = OpSConvert %[[#I64]] %[[#]]
; CHECK:             %[[#T1_OFFSET0:]] = OpIMul %[[#I64]] %[[#T1_M]] %[[#I64_C1]]
; CHECK:             %[[#T1_SUBPTR1:]] = OpInBoundsPtrAccessChain %[[#I32_PTR]] %[[#T1_POINTER]] %[[#T1_OFFSET0]]
; CHECK-NEXT:                            OpStore %[[#T1_SUBPTR1]] %[[#]]
; CHECK-NEXT:        %[[#T1_OFFSET1:]] = OpIAdd %[[#I64]] %[[#T1_OFFSET0]] %[[#I64_C64]]
; CHECK-NEXT:        %[[#T1_SUBPTR2:]] = OpInBoundsPtrAccessChain %[[#I32_PTR]] %[[#T1_POINTER]] %[[#T1_OFFSET1]]
; CHECK-NEXT:                            OpStore %[[#T1_SUBPTR2]] %[[#]]
; CHECK-NEXT:        %[[#T1_OFFSET2:]] = OpIAdd %[[#I64]] %[[#T1_OFFSET1]] %[[#I64_C64]]
; CHECK-NEXT:        %[[#T1_SUBPTR3:]] = OpInBoundsPtrAccessChain %[[#I32_PTR]] %[[#T1_POINTER]] %[[#T1_OFFSET2]]
; CHECK-NEXT:                            OpStore %[[#T1_SUBPTR3]] %[[#]]
}

